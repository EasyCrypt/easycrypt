require import AllCore List Distr DBool CHoareTactic.
(*   *) import StdOrder RField RealOrder StdBigop Bigreal.

type from.
type hash.

(* Remark : this implies that hash is a finite type *)
op [lossless uniform full] dhash : hash distr.

clone import MUniFinFun as MUFF with
  type t <- from.   (* This assume that the type of from is finite *)

(* Uniform distribution over from -> hash *)
op dfhash : (from -> hash) distr = dfun (fun _ => dhash).

lemma dfhash_ll: is_lossless dfhash.
proof. apply dfun_ll => ?;apply dhash_ll. qed.

lemma dfhash_uni: is_uniform dfhash.
proof. apply dfun_uni => ?; apply dhash_uni. qed.

lemma dfhash_fu: is_full dfhash.
proof. apply dfun_fu => ?; apply dhash_fu. qed.

hint solve 0 random : dfhash_ll dfhash_uni dfhash_fu.

module type QRO_i = {
  proc init () : unit
  quantum proc h {_:from} : hash    
}.

module type QRO = {
  include QRO_i [h]
}.

module QRO : QRO_i = {
  var  h : from -> hash

  proc init() = { h <$ dfhash; }

  quantum proc h {x:from} = { return h x; }

}.

(* -------------------------------------------------------------------------- *)
(* Biased distribution over boolean return true with probability p *)   
clone MUniFinFunBiased as DBB with 
  type t <- from,
  op MUniFinFun.FinT.enum <-  MUFF.FinT.enum
  proof *.
realize MUniFinFun.FinT.enum_spec by apply MUFF.FinT.enum_spec.

(* -------------------------------------------------------------------------- *)
(* Semi Constant Definition Property *)
abstract theory SemiConstDistr.

op k : int.

type result = bool * from * from list.

op good(bf : from -> bool, x : from, l : from list) = 
     bf x /\ (forall x', x' \in l => !bf x') /\ size l <= k.

quantum module type AdvSCD (H:QRO) = {
  proc main() : result
}.
 
module SCD (A:AdvSCD) = {
  var bf : from -> bool
  var x : from
  var l : from list

  proc _F0(p:real) = {
    var c, y;
    bf <$ DBB.dbfun p;
    QRO.init();
    y  <$ dhash; 
    (c,x,l)  <@ A(QRO).main();
    return (c /\ good bf x l);
  }

  proc _F1(p:real) = {
    var c, y;
    bf <$ DBB.dbfun p;
    QRO.init();
    y  <$ dhash; 
    QRO.h <- fun m => if bf m then y else QRO.h m;
    (c,x,l)  <@ A(QRO).main();
    return (c /\ good bf x l);
  }
}.

axiom advantage q lambda (A<:AdvSCD{-SCD}[main : `{Inf, #H.h : q}]) &m:
  0%r <= lambda <= 1%r =>
  `| Pr[SCD(A)._F0(lambda) @ &m : res] - Pr[SCD(A)._F1(lambda) @ &m: res] | 
   <= (2%r * q%r + k%r + 1%r)/ 6%r * lambda^2.

end SemiConstDistr.

quantum module type AdvRO (H:QRO) = {
  proc main() : bool
}.

module IND_QRO (H:QRO_i, A:AdvRO) = {
  proc main() : bool = {
    var b;

    H.init();
    b <@ A(H).main();
    return b;
  }
}.

abstract theory T_PRF.

type key.
op dkey : key distr.
op F : key -> from -> hash.

module PRF : QRO_i = {
  var k : key
  proc init() = { k <$ dkey; }
  quantum proc h{x:from} = { return F k x; } 
}.

(* The prf advantage is defined by: 
  `| Pr[IND_QRO(PRF, A).main() @ &m : res] - Pr[IND_QRO(QRO, A).main() @ &m : res] |
*)

end T_PRF.

type seed.

op [lossless uniform full] dF : (from -> seed) distr.
op G : seed -> hash.

abstract theory T_OracleSecurePRG.

(* G is an oracle-secure PRG if (G âˆ˜ F) is a PRF for random F. *)
module PRF : QRO_i = {
  var f : from -> seed
  proc init() = { f <$ dF; }
  quantum proc h {x : from} = { return G (f x); }
}.

(* G is oracle-secure if for polynomial q,
axiom advantage q (A <: AdvRO[main: `{Inf, #H.h : q}]) &m:
`| Pr[IND_QRO(PRF, A).main() @ &m : res] - Pr[IND_QRO(QRO, A).main() @ &m : res] | <= negl.
*)

end T_OracleSecurePRG.

op [lossless uniform full] dseed : seed distr.

abstract theory T_StandardSecurePRG.

(* random number generation *)
module type RGi = {
  proc init() : unit
  proc sample() : hash
}.

module type RG = {
  include RGi [sample]
}.

(* adversary *)
quantum module type AdvPRG (G : RG) = {
  proc main() : bool
}.

module IND_PRG(G : RGi, D : AdvPRG) = {
  proc main() : bool = {
    var b;
    G.init();
    b <@ D(G).main();
    return b;
  }
}.

module PRG : RGi = {
  var s : seed
  proc init() = {
    s <$ dseed;
  }
  proc sample() = {
    return G s;
  }
}.

module RG : RGi = {
  var r : hash
  proc init() = {
    r <$ dhash;
  }
  proc sample() = {
    return r;
  }
}.

(* G is standard-secure if for polynomial q,
axiom advantage q (A <: AdvPRG[main: `{Inf, #G.sample : q}]) &m:
`| Pr[IND_PRG(PRG, A).main() @ &m : res] - Pr[IND_PRG(RG, A).main() @ &m : res] | <= negl.
*)

end T_StandardSecurePRG.

(* TODO: if G is standard-secure, then it is also oracle-secure. *)


(* Efficient QROM simulation *)

require (****) Matrix.
abstract theory T_LazyQROM.

op q : int.

axiom q_ge0 : 0 <= q.

(* we need an efficient bijection that relates
   from/hash to a finite field *)
type F.

op from2f : from -> F.
op f2from : F -> from.

axiom from2fB : bijective from2f.
axiom from2fK : cancel from2f f2from.
lemma f2fromK : cancel f2from from2f by smt (bij_can_sym from2fB from2fK).
lemma f2fromB : bijective f2from by  smt (bij_can_bij from2fB from2fK).

op hash2f : hash -> F.
op f2hash : F -> hash.

axiom hash2fB : bijective hash2f.
axiom hash2fK : cancel hash2f f2hash.
lemma f2hashK : cancel f2hash hash2f by smt (bij_can_sym hash2fB hash2fK).
lemma f2hashB : bijective f2hash by  smt (bij_can_bij hash2fB hash2fK).

clone import Matrix with
  type R <- F,
  op size <- 2*q
  proof ge0_size by smt(q_ge0).



end T_LazyQROM.
