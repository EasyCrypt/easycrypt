require import AllCore List Distr DBool CHoareTactic IntDiv.
(*   *) import StdOrder RField RealOrder StdBigop Bigreal BRA.
require (*  *) Matrix Tuple (* FinType *).

type from.
type hash.

(* Remark : this implies that hash is a finite type *)
op [lossless uniform full] dhash : hash distr.

clone import MUniFinFun as MUFF with
  type t <- from.   (* This assume that the type of from is finite *)

(* Uniform distribution over from -> hash *)
op dfhash : (from -> hash) distr = dfun (fun _ => dhash).

lemma dfhash_ll: is_lossless dfhash.
proof. apply dfun_ll => ?;apply dhash_ll. qed.

lemma dfhash_uni: is_uniform dfhash.
proof. apply dfun_uni => ?; apply dhash_uni. qed.

lemma dfhash_fu: is_full dfhash.
proof. apply dfun_fu => ?; apply dhash_fu. qed.

hint solve 0 random : dfhash_ll dfhash_uni dfhash_fu.

module type QRO_i = {
  proc init () : unit
  quantum proc h {_:from} : hash    
}.

module type QRO = {
  include QRO_i [h]
}.

module QRO : QRO_i = {
  var  h : from -> hash

  proc init() = { h <$ dfhash; }

  quantum proc h {x:from} = { return h x; }

}.

(* -------------------------------------------------------------------------- *)
(* Biased distribution over boolean return true with probability p *)   
clone MUniFinFunBiased as DBB with 
  type t <- from,
  op MUniFinFun.FinT.enum <-  MUFF.FinT.enum
  proof *.
realize MUniFinFun.FinT.enum_spec by apply MUFF.FinT.enum_spec.

(* -------------------------------------------------------------------------- *)
(* Semi Constant Definition Property *)
abstract theory SemiConstDistr.

op k : int.

type result = bool * from * from list.

op good(bf : from -> bool, x : from, l : from list) = 
     bf x /\ (forall x', x' \in l => !bf x') /\ size l <= k.

quantum module type AdvSCD (H:QRO) = {
  proc main() : result
}.
 
module SCD (A:AdvSCD) = {
  var bf : from -> bool
  var x : from
  var l : from list

  proc _F0(p:real) = {
    var c, y;
    bf <$ DBB.dbfun p;
    QRO.init();
    y  <$ dhash; 
    (c,x,l)  <@ A(QRO).main();
    return (c /\ good bf x l);
  }

  proc _F1(p:real) = {
    var c, y;
    bf <$ DBB.dbfun p;
    QRO.init();
    y  <$ dhash; 
    QRO.h <- fun m => if bf m then y else QRO.h m;
    (c,x,l)  <@ A(QRO).main();
    return (c /\ good bf x l);
  }
}.

axiom advantage q lambda (A<:AdvSCD{-SCD}[main : `{Inf, #H.h : q}]) &m:
  0%r <= lambda <= 1%r =>
  `| Pr[SCD(A)._F0(lambda) @ &m : res] - Pr[SCD(A)._F1(lambda) @ &m: res] | 
   <= (2%r * q%r + k%r + 1%r)/ 6%r * lambda^2.

end SemiConstDistr.

quantum module type AdvRO (H:QRO) = {
  proc main() : bool
}.

module IND_QRO (H:QRO_i, A:AdvRO) = {
  proc main() : bool = {
    var b;

    H.init();
    b <@ A(H).main();
    return b;
  }
}.

abstract theory T_PRF.

type key.
op dkey : key distr.
op F : key -> from -> hash.

module PRF : QRO_i = {
  var k : key
  proc init() = { k <$ dkey; }
  quantum proc h{x:from} = { return F k x; } 
}.

(* The prf advantage is defined by: 
  `| Pr[IND_QRO(PRF, A).main() @ &m : res] - Pr[IND_QRO(QRO, A).main() @ &m : res] |
*)

end T_PRF.

type seed.

op [lossless uniform full] dF : (from -> seed) distr.
op G : seed -> hash.

abstract theory T_OracleSecurePRG.

(* G is an oracle-secure PRG if (G âˆ˜ F) is a PRF for random F. *)
module PRF : QRO_i = {
  var f : from -> seed
  proc init() = { f <$ dF; }
  quantum proc h {x : from} = { return G (f x); }
}.

(* G is oracle-secure if for polynomial q,
axiom advantage q (A <: AdvRO[main: `{Inf, #H.h : q}]) &m:
`| Pr[IND_QRO(PRF, A).main() @ &m : res] - Pr[IND_QRO(QRO, A).main() @ &m : res] | <= negl.
*)

end T_OracleSecurePRG.

op [lossless uniform full] dseed : seed distr.

abstract theory T_StandardSecurePRG.

(* random number generation *)
module type RGi = {
  proc init() : unit
  proc sample() : hash
}.

module type RG = {
  include RGi [sample]
}.

(* adversary *)
quantum module type AdvPRG (G : RG) = {
  proc main() : bool
}.

module IND_PRG(G : RGi, D : AdvPRG) = {
  proc main() : bool = {
    var b;
    G.init();
    b <@ D(G).main();
    return b;
  }
}.

module PRG : RGi = {
  var s : seed
  proc init() = {
    s <$ dseed;
  }
  proc sample() = {
    return G s;
  }
}.

module RG : RGi = {
  var r : hash
  proc init() = {
    r <$ dhash;
  }
  proc sample() = {
    return r;
  }
}.

(* G is standard-secure if for polynomial q,
axiom advantage q (A <: AdvPRG[main: `{Inf, #G.sample : q}]) &m:
`| Pr[IND_PRG(PRG, A).main() @ &m : res] - Pr[IND_PRG(RG, A).main() @ &m : res] | <= negl.
*)

end T_StandardSecurePRG.

(* TODO: if G is standard-secure, then it is also oracle-secure. *)

(* 
    Mark Zhandry
    Secure Identity-Based Encryption in the Quantum Random Oracle Model
    https://eprint.iacr.org/2012/076.pdf
    Theorem 3.1.  *)

abstract theory QROM_Fundamental_Lemma.

clone import Tuple as TupleXY with 
  type t <- from * hash.

op q : int.

axiom q_ge0 : 0 <= q.


type result.

quantum module type AdvRO (H:QRO) = {
  proc main() : result
}.

module QRO_main_D(A : AdvRO) = {
  proc main(adfhash : (from -> hash) distr) = {
    var r;
    QRO.h <$ adfhash;
    r <@ A(QRO).main();
    return r;
  }
}.

module QRO_main(A : AdvRO, H : QRO_i) = {
  proc main() = {
    var r;
    H.init();
    r <@ A(H).main();
    return r;
  }
}.

op C : (from * hash) list -> result -> real.

axiom dA_split (A<:AdvRO{-QRO_main}[main : `{Inf, #H.h : q}]) &m:
   forall (adfhash: (from -> hash) distr) (r:result),
    Pr[ QRO_main_D(A).main(adfhash) @ &m : res = r] = 
    big predT (fun l => 
      C l r * mu adfhash (fun fx => all (fun (xr:_*_) => fx xr.`1 = xr.`2) l))
        (wordn (2*q)).

end QROM_Fundamental_Lemma.

(* Efficient QROM simulation: Theorem 6.1 in 
    Mark Zhandry
    Secure Identity-Based Encryption in the Quantum Random Oracle Model
    https://eprint.iacr.org/2012/076.pdf  *)

(* The theorem can be generalized to capture any QROM of the following form:

   |X| < p^n
   |Y| = p^m

s.t. 1 < m <= n and 1 < p

The idea is that the linear independence argument gives a random element in FF_p^n,
which can be converted in a random element in FF_p^m by raising to p^{n-m}.

This means, in particular, that we can model any possible compressing or
length preserving QROM over bit strings and, in particular, random oracles
that map bit strings to a single bit.

*)

abstract theory T_LazyQROM.

op q : int.

axiom q_ge0 : 0 <= q.

(* Sampling is based on two finite fields with the same characteristic. *)
op p : { int | 1 < p } as gt1_p.

type ff_in.
op enum_ff_in : ff_in list.
op n : { int | 0 < n } as gt0_n.
axiom ff_in_order : size enum_ff_in = p^n.

clone  MFinite as FFin with
  type t <- ff_in,
  op Support.enum <- enum_ff_in.

lemma in_char_order : p %| FFin.Support.card. 
have -> : (p = p^1); first by smt(@Ring.IntID).
by rewrite /card  ff_in_order; apply dvdz_exp2l; smt(gt0_n).
qed.

type ff_out.
op enum_ff_out : ff_out list.
op m : { int | 0 < m <= n } as bnd_m.
axiom ff_out_order : size enum_ff_out = p^m.

clone  MFinite as FFout with
  type t <- ff_out,
  op Support.enum <- enum_ff_out.

lemma out_char_order : p %| FFout.Support.card. 
have -> : (p = p^1); first by smt(@Ring.IntID).
by rewrite /card  ff_out_order; apply dvdz_exp2l; smt(bnd_m).
qed.


(* Injection means that you have an FF that is large enough
   to encode all possible inputs *)
op encode : from -> ff_in.
axiom encodeB : injective encode.

(* Bijection means that a uniform field element gives you a uniform
   element in hash, which is all we need *)
op decode : ff_out -> hash.
axiom decodeB : bijective decode.

(* Note that this will work for any hash type that has p^n elements,
   for p prime and n > 0 some integer. And |from| <= |hash|. *)

clone import Matrix with
  type R <- ff_in,
  op size <- 2*q
  proof ge0_size by smt(q_ge0).

op genseed = dvector FFin.dunifin.

(* We use exponentiation to generate a row in a Vandermonde matrix *)
op (^) : ff_in -> int -> ff_in.

(* We use a projection function to obtain a value in ff_out *)

op project : ff_in -> ff_out.

axiom project_uniform y :
   size (filter (fun x => project x = y) enum_ff_in) = p^(n-m).

op compute(seed : vector, x : from) : hash =
        let xv = offunv (fun (i : int) => encode x^i) in
        let yf = dotp xv seed in
            decode (project yf).

module LQRO : QRO_i = {
  var seed : vector

  proc init() = { seed <$ genseed; }

  quantum proc h {x:from} = { return compute seed x; }

}.

op dcompute : (from -> hash) distr = 
       dmap genseed (fun seed => compute seed).

clone import QROM_Fundamental_Lemma with
    op q <- q
    proof q_ge0 by smt(q_ge0).
import TupleXY.

(* This requires some random sampling magic *)

lemma eager_sampling  (A<:AdvRO{-QRO, -LQRO}[main : `{Inf, #H.h : q}]) &m (r : result):
  Pr[ QRO_main(A,LQRO).main() @ &m: res = r] = Pr[ QRO_main_D(A).main(dcompute) @ &m: res = r].
admitted.

(* This axiom captures the following property on the Vandermonde 
   matrix:  any 2q rows are linearly independent. *)

op uhash_list(xs : ff_in list, seed : vector) : vector = 
   offunv (fun i =>
              let x = nth witness xs i in
              let xv = offunv (fun (j : int) =>  x^j) in
                  dotp xv seed).

axiom indep2q xs: 
  size xs = 2*q =>
  uniq xs =>
    bijective (uhash_list xs).

lemma universal2q xs :
  size xs = 2*q =>
  uniq xs =>
  genseed = dmap genseed (uhash_list xs).
move => sxs uxs.
apply eq_distr => x.
have : (is_funiform genseed); first
   by rewrite /genseed; apply (dvector_funi _ FFin.dunifin_fu FFin.dunifin_uni).
move => genseed_funi.
have : (is_funiform (dmap genseed (uhash_list xs)));
 first by apply (dmap_funi _ _ (indep2q xs sxs uxs) genseed_funi).
move => uhash_funiform.
(* not sure how to prove this, which is obviously true *)
admitted.

lemma perfect_sim xy:
  xy \in wordn (2 * q) =>
 mu dfhash (fun (fx : from -> hash) => all (fun (xr : from * hash) => fx xr.`1 = xr.`2) xy) =
 mu dcompute (fun (fx : from -> hash) => all (fun (xr : from * hash) => fx xr.`1 = xr.`2) xy).
admitted.

lemma efficient_sim (A<:AdvRO{-QRO, -LQRO}[main : `{Inf, #H.h : q}]) &m (r : result):
  Pr[ QRO_main(A,QRO).main() @ &m: res = r ] = Pr[ QRO_main(A,LQRO).main() @ &m: res = r ].
proof.
have -> : 
 Pr[ QRO_main(A,QRO).main() @ &m: res = r ] = Pr[QRO_main_D(A).main(dfhash) @ &m : res = r].
   by byequiv=>//; conseq (_: _ ==> ={res})=> //; proc;inline*; sim; auto => />.
rewrite (eager_sampling A) (dA_split A &m dfhash r) (dA_split A &m dcompute r).
by apply (eq_big_seq _ _  (wordn (2*q))); move => * /=;congr; apply perfect_sim.
qed.

end T_LazyQROM.
